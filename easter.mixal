* MIX Assembly Language, GNU MDK 1.2.9
*
* Print a table showing date of Easter for 2000 to 2050
*
GOLD    EQU     100     golden number in Metonic cycle
CENT    EQU     101     century number
CORX    EQU     102     corrections for dropped leap days
CORZ    EQU     103     corrections for full moon
SUND    EQU     104     identify Sunday
EPCT    EQU     105     epact
DATE    EQU     106     date in March
YEAR    EQU     107     current year
CBUF    EQU     108     current buffer
TERM    EQU     19      stdout
BUF1    EQU     400     two buffers for writing output
BUF2    EQU     425
*
        ORIG    3000
* set starting year
START   ENTA    2000
        STA     YEAR
* initialize the buffers
        LDA     BUF2
        STA     BUF1 + 24
        LDA     BUF1
        STA     BUF2 + 24
* set the current buffer
        STA     CBUF
* print title of table
        OUT     TITLE(TERM)
* begin date calculations for current year
* find golden number
CALC    ENTA    0
        LDX     YEAR
        DIV     =19=
        INCX    1
        STX     GOLD    golden number
* find century
        ENTA    0
        LDX     YEAR
        DIV     =100=
        INCA    1
        STA     CENT
* find corrections
        ENTA    3
        MUL     CENT
        DIV     =4=
        DECA    12
        STA     CORX
        ENTA    8
        MUL     CENT
        INCX    5
        DIV     =25=
        DECA    5
        STA     CORZ
* find sunday
        ENTA    5
        MUL     YEAR
        DIV     =4=
        SUB     CORX
        DECA    10
        STA     SUND
* find epact
        ENTA    11
        MUL     GOLD
        SLAX    5
        INCA    20
        ADD     CORZ
        SUB     CORX
        SRAX    5
        DIV     =30=
        SLAX    5
        CMPA    =25=
        JNE     2F
        ENTX    11
        CMPX    GOLD
        JLE     4F
        INCA    1
        JSJ     4F
2H      CMPA    =24=
        JNE     4F
        INCA    1
4H      STA     EPCT
* find full moon
        ENTA    44
        SUB     EPCT
        CMPA    =21=
        JGE     2F
        INCA 30
* advance to sunday
2H      STA     DATE
        ADD     SUND
        SRAX    5
        DIV     =7=
        SLAX    5
        MUL     =-1=
        SLAX    5
        ADD     DATE
        INCA    7
* check to see which month we are in
        CMPA    =31=
        JG      2F
* we are in march
        CHAR
        SLAX    8
        ADD     =897=           MA
        STA     CBUF
        ENT1    1
        LDA     YEAR
        CHAR
        ENTA    19              R
        SLA     2
        ADD     =200=           CH
        SLA     1
        SLAX    1
        STA     CBUF,1
        INC1    1
        SLAX    5
        STA     CBUF,1
        OUT     CBUF(TERM)
        LDA     CBUF + 42
        STA     CBUF            change buffers
        JSJ     4F
* we are in april
2H      DECA    31
        CHAR
        SLAX    8
        ADD     =81=            AP
        STA     CBUF
        ENT1    1
        LDA     YEAR
        CHAR
        ENTA    19              R
        SLA     2
        ADD     =589=           IL
        SLA     1
        SLAX    1
        STA     CBUF,1
        INC1    1
        SLAX    5
        STA     CBUF,1
        OUT     CBUF(TERM)
        LDA     CBUF + 42
        STA     CBUF            change buffers
* see if we are at the end of the table
4H      LDA     YEAR
        CMPA     =2050=
        JE      6F
* not at end of table, go to next line
        INCA    1
        STA     YEAR
        JSJ     CALC
6H      HLT
TITLE   ALF     "DATES"
        ALF     " OF E"
        ALF     "ASTER"
        ALF     " FROM"
        ALF     "2000 "
        ALF     "TO 20"
        ALF     "50   "
        END     START
